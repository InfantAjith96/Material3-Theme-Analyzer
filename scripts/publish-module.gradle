apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

// Define the necessary variables
def PUBLISH_GROUP_ID = 'com.example.material3theme'  // Replace with your group ID
def PUBLISH_VERSION = '1.0.0'  // Replace with your version
def PUBLISH_ARTIFACT_ID = 'materialtheme'  // Replace with your artifact ID
def PUBLISH_DESCRIPTION = 'Material 3 Theme Analyzer for Android'
def PUBLISH_URL = 'https://github.com/InfantAjith96/Material3-Theme-Analyzer'
def PUBLISH_LICENSE_NAME = 'Apache-2.0'
def PUBLISH_LICENSE_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
def PUBLISH_DEVELOPER_ID = 'infantajith1996'
def PUBLISH_DEVELOPER_NAME = 'Ajith'
def PUBLISH_DEVELOPER_EMAIL = 'infantajith1996@gmail.com'
def PUBLISH_SCM_CONNECTION = 'scm:git:https://github.com/InfantAjith96/Material3-Theme-Analyzer.git'
def PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:https://github.com/InfantAjith96/Material3-Theme-Analyzer.git'
def PUBLISH_SCM_URL = 'https://github.com/InfantAjith96/Material3-Theme-Analyzer'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = PUBLISH_URL
                    licenses {
                        license {
                            name = PUBLISH_LICENSE_NAME
                            url = PUBLISH_LICENSE_URL
                        }
                    }
                    developers {
                        developer {
                            id = PUBLISH_DEVELOPER_ID
                            name = PUBLISH_DEVELOPER_NAME
                            email = PUBLISH_DEVELOPER_EMAIL
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = PUBLISH_SCM_CONNECTION
                        developerConnection = PUBLISH_SCM_DEVELOPER_CONNECTION
                        url = PUBLISH_SCM_URL
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}